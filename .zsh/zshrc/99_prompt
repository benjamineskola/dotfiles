# -*- zsh -*-

case $TERM in
    xterm*|screen*)
        print -Pn "\e]0;%n@%m:%~\a"
        preexec () { print -Pn "\e]0;%n@%m:%~> $1\a"; }
        ;;
esac

function vcs_info {
       _vcs_info=''
       hg root >/dev/null 2>&1 && _vcs_info=" %F{cyan}hg:%F{magenta}$(hg branch 2> /dev/null)%f"
       command git branch >/dev/null 2>&1 && _vcs_info=" %F{cyan}git:%F{magenta}${$(command git symbolic-ref HEAD)#refs/heads/}%f"
}

function ruby_info {
       _ruby_info=" ${${${$(ruby --version)##ruby }%% *}%%p*}"
}

autoload add-zsh-hook
add-zsh-hook precmd vcs_info
add-zsh-hook precmd ruby_info

setopt prompt_subst
PROMPT='[%(!.%F{red}.%F{blue})%n%f@%F{yellow}%m%f %F{green}${${:-${${${(@j:/:M)${(@s:/:)${(%):-%~}}##.#?}:h}%/}/}#./}${${(%):-%1~}#/}%f${_vcs_info}${_ruby_info}]%0(?.%F{green}✓.%F{red}✗)%f '
